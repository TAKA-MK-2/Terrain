// Each #kernel tells which function to compile; you can have many kernels
// 初期化
#pragma kernel Init
// エミット
#pragma kernel Emit
// 更新
#pragma kernel Update

#define THREAD_NUM_X 8

struct GPUParticleData
{
	// アクティブか判断する
	bool isActive;
	// 座標
	float3 position;
	// 速度
	float3 velocity;
	float3 rotation;
	float3 angVelocity;
	// サイズ
	float scale;
	// 生存時間
	float lifeTime;
	// 経過時間
	float elapsedTime;
};

// 乱数生成
inline float rnd(float2 p)
{
	return frac(sin(dot(p, float2(12.9898, 78.233))) * 43758.5453);
}

inline float3 rnd3(float2 p) 
{
	return 2.0 * (float3(rnd(p * 1), rnd(p * 2), rnd(p * 3)) - 0.5);
}

float3 hsv_to_rgb(float3 HSV)
{
	float4 k = float4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	float3 p = abs(frac(HSV.xxx + k.xyz) * 6.0 - k.www);
	return HSV.z * lerp(k.xxx, clamp(p - k.xxx, 0.0, 1.0), HSV.y);
}

RWStructuredBuffer<GPUParticleData> _particles;
AppendStructuredBuffer<uint> _deadList;
ConsumeStructuredBuffer<uint> _particlePool;
AppendStructuredBuffer<uint> _activeList;

float3 _emitPosition;
float3 _velocity;
float3 _rotation;
float3 _angVelocity;
float _deltaTime;
float _elapsedTime;
float _lifeTime;
float _scaleMin;
float _scaleMax;
float _gravity;
float _sai;
float _val;
float3 _range;
float3 _direction;

[numthreads(THREAD_NUM_X, 1, 1)]
void Init(uint3 id : SV_DispatchThreadID)
{
	uint no = id.x;

	_particles[no].isActive = false;
	_deadList.Append(no);
}

[numthreads(THREAD_NUM_X, 1, 1)]
void Emit()
{
	uint no = _particlePool.Consume();
	float2 seed = float2(no + _elapsedTime, no + 1 + _elapsedTime);
	float3 position = rnd3(seed);
	float3 rotation = rnd3(seed + 1);
	float3 velocity = rnd3(seed + 2);
	float scale = rnd(seed + 3) * (_scaleMax - _scaleMin) + _scaleMin;

	_particles[no].isActive = true;
	_particles[no].position = _emitPosition + (position * _range);
	_particles[no].velocity = _velocity * velocity;
	_particles[no].velocity.y = abs(_particles[no].velocity.y);
	_particles[no].rotation = _angVelocity * rotation;
	_particles[no].angVelocity = _angVelocity * rotation;
	_particles[no].scale = scale;
	_particles[no].lifeTime = _lifeTime * rnd(seed + 3);
	_particles[no].elapsedTime = 0;
}

[numthreads(THREAD_NUM_X, 1, 1)]
void Update(uint3 id : SV_DispatchThreadID)
{
	uint no = id.x;

	if (_particles[no].isActive)
	{
		_particles[no].elapsedTime += _deltaTime;
		_particles[no].position += _particles[no].velocity * _deltaTime;
		_particles[no].velocity.y -= _gravity * _deltaTime;
		_particles[no].rotation += _particles[no].angVelocity * _deltaTime;
		_particles[no].scale = _scaleMax * (1 - pow(_particles[no].elapsedTime / _particles[no].lifeTime, 3.0));
		if (_particles[no].lifeTime <= _particles[no].elapsedTime)
		{
			_particles[no].isActive = false;
			_deadList.Append(no);
		}
		else
		{
			_activeList.Append(no);
		}
	}

}
