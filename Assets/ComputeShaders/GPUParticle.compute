// 初期化
#pragma kernel Init
// エミット
#pragma kernel Emit
// 更新
#pragma kernel Update

// スレッド数
#define THREAD_NUM_X 32

// パーティクル情報
struct GPUParticleData
{
	// アクティブか判断する
	bool isActive;
	// 座標
	float3 position;
	// 速度
	float3 velocity;
	// スケール
	float scale;
	// 開始スケール
	float startScale;
	// 最終スケール
	float endScale;
	// 色
	float4 color;
	// 生存時間
	float lifeTime;
	// 経過時間
	float elapsedTime;
};

// floatの乱数生成(0～1)
inline float rnd(float2 p)
{
	return frac(sin(dot(p, float2(12.9898, 78.233))) * 43758.5453);
}

// float3の乱数生成(-1～1)
inline float3 rnd3(float2 p) 
{
	return 2.0 * (float3(rnd(p * 1), rnd(p * 2), rnd(p * 3)) - 0.5);
}

// パーティクル情報
RWStructuredBuffer<GPUParticleData> _particlesBuffer;
// 使用中のパーティクルの要素番号
AppendStructuredBuffer<uint> _activeParticlesBuffer;
// 未使用のパーティクルの要素番号
AppendStructuredBuffer<uint> _deadParticlesBuffer;
ConsumeStructuredBuffer<uint> _particlePoolBuffer;

// 重力
float _gravity;
// 経過時間
float _elapsedTime;
// 座標
float3 _position;
// エミッターのサイズ
float3 _range;
// 最低速度
float3 _minVelocity;
// 最高速度
float3 _maxVelocity;
// 開始スケール
float _startScale;
// 最終スケール
float _endScale;
// 色情報
float3 _color;
// 生存時間
float _lifeTime;
// フレーム時間
float _deltaTime;

// 初期化処理
[numthreads(THREAD_NUM_X, 1, 1)]
void Init(uint id : SV_DispatchThreadID)
{
	// 未使用リストに追加
	_particlesBuffer[id].isActive = false;
	_deadParticlesBuffer.Append(id);
}

// エミット処理
[numthreads(THREAD_NUM_X, 1, 1)]
void Emit()
{
	// 要素番号をパーティクルプールから取り出す
	uint id = _particlePoolBuffer.Consume();

	// バッファからパーティクル情報を取り出す
	GPUParticleData particle = _particlesBuffer[id];

	// 値をランダム化
	float2 seed = float2(id, id + 1) * _elapsedTime % 10000;

	// パーティクルを設定
	particle.isActive = true;
	particle.position = _position + (rnd3(seed) * _range);
	particle.velocity = (_maxVelocity - _minVelocity) * abs(rnd3(seed + 1)) + _minVelocity;
	particle.scale = _startScale;
	particle.startScale = _startScale;
	particle.endScale = _endScale;
	particle.color = float4(_color, 1);
	particle.lifeTime = _lifeTime * rnd(seed + 2);
	particle.elapsedTime = 0;

	// バッファに再設定
	_particlesBuffer[id] = particle;
}

// 更新処理
[numthreads(THREAD_NUM_X, 1, 1)]
void Update(uint id : SV_DispatchThreadID)
{
	// バッファからパーティクル情報を取り出す
	GPUParticleData particle = _particlesBuffer[id];

	// アクティブか判定
	if (particle.isActive)
	{
		// パーティクルの更新処理
		particle.position += particle.velocity * _deltaTime;
		particle.scale = pow(particle.elapsedTime / particle.lifeTime, 3.0) * (particle.endScale - particle.startScale) + particle.startScale;
		particle.velocity.y -= _gravity * _deltaTime;
		particle.elapsedTime += _deltaTime;

		// 消滅時間になったか判定
		if (particle.lifeTime < particle.elapsedTime)
		{
			// パーティクルを非アクティブにする
			particle.isActive = false;
			// 未使用リストに追加
			_deadParticlesBuffer.Append(id);
		}
		else
		{
			// 使用中リストに追加
			_activeParticlesBuffer.Append(id);
		}
	}

	// バッファに再設定
	_particlesBuffer[id] = particle;
}
