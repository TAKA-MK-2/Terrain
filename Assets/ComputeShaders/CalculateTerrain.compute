#pragma kernel CalculateVertices
#pragma kernel PutFootsteps

// スレッド数
#define THREAD_NUM 8
// プレイヤーの半径
#define PLAYER_RADIUS 5

// 頂点座標
RWStructuredBuffer<float3> _verticesBuffer;

// フィールドサイズ
float _fieldSize;
// 頂点数
int _numVertices;
// 頂点間の距離
float _distance;
// 高さ
float _height;
// ノイズの滑らかさ
float _smoothness;
// ノイズの取得位置
float2 _offset;
// プレイヤーの座標
float2 _playerPosition;

// float2の乱数生成
float2 random2(float2 st) 
{
	st = float2(dot(st, float2(127.1, 311.7)),dot(st, float2(269.5, 183.3)));
	return -1.0 + 2.0*frac(sin(st)*43758.5453123);
}

// パーリンノイズ計算
float perlinNoise(float2 st)
{
	float2 p = floor(st);
	float2 f = frac(st);
	float2 u = f * f * (3.0 - 2.0 * f);

	float v00 = random2(p + float2(0, 0));
	float v10 = random2(p + float2(1, 0));
	float v01 = random2(p + float2(0, 1));
	float v11 = random2(p + float2(1, 1));

	return lerp(
		lerp(dot(v00, f - float2(0, 0)), dot(v10, f - float2(1, 0)), u.x),
		lerp(dot(v01, f - float2(0, 1)), dot(v11, f - float2(1, 1)), u.x),
		u.y) + 0.5f;
}

// fBmノイズ計算
float fBm(float2 st)
{
	float f = 0;
	float2 q = st;

	f += 0.5000 * perlinNoise(q); 
	q = q * 2.01;
	f += 0.2500 * perlinNoise(q);
	q = q * 2.02;
	f += 0.1250 * perlinNoise(q); 
	q = q * 2.03;
	f += 0.0625 * perlinNoise(q);
	q = q * 2.01;

	return f;
}

// 頂点計算
[numthreads(THREAD_NUM, 1, THREAD_NUM)]
void CalculateVertices(uint3 id : SV_DispatchThreadID)
{
	// バッファの要素番号の計算
	int index = (id.z * _numVertices) + id.x;
	// ノイズの取得
	float2 seed = float2(id.x + _offset.x, id.z + _offset.y) / _smoothness;
	float noise = perlinNoise(seed);
	// 頂点座標の計算
	float3 vertex;
	vertex.x = id.x * _distance + (_distance / 2.0) - (_fieldSize / 2.0);
	vertex.y = _height * noise;
	vertex.z = id.z * _distance + (_distance / 2.0) - (_fieldSize / 2.0);
	_verticesBuffer[index] = vertex;
}

[numthreads(THREAD_NUM * THREAD_NUM, 1, 1)]
void PutFootsteps(uint id : SV_DispatchThreadID)
{
	// 頂点座標の計算
	float3 vertex = _verticesBuffer[id];
	// 三平方の定理
	double horizontal = (vertex.x - _playerPosition.x) * (vertex.x - _playerPosition.x);
	double vertical = (vertex.z - _playerPosition.y) * (vertex.z - _playerPosition.y);
	double slant = PLAYER_RADIUS * PLAYER_RADIUS;
	// 足跡内にあるか判定
	if (horizontal + vertical <= slant)
	{
		vertex.y -= PLAYER_RADIUS;
	}
	_verticesBuffer[id] = vertex;
}